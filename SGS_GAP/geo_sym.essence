language ESSENCE 2.4.0

given n : int
given sizeS : int
letting N be domain int(0..n-1)
given SGS : function (total) (int(0..sizeS), N) --> N

find M : function (total) (N,N) --> N
find J : function (total) (N,N) --> N
find R : function (total) N --> N
find C : function (total) (N,N) --> int(0..1)

such that

$Associative
forAll i,j,k: N. M((i,M((j,k)))) = M((M((i,j)),k)),
forAll i,j,k: N. J((i,J((j,k)))) = J((J((i,j)),k)),

$Commutative
forAll i,j: N. M((i,j)) = M((j,i)),
forAll i,j: N. J((i,j)) = J((j,i)),

$Absorption
forAll i,j: N. M((i,J((i,j)))) = i,
forAll i,j: N. J((i,M((i,j)))) = i,

$Idempotent
forAll i: N. M((i,i)) = i,
forAll i: N. J((i,i)) = i,

$Bounded
forAll i : N. M((n-1,i)) = i,
forAll i : N. J((0,i)) = i,

$Symmetry breaking 
forAll i,j : N. (R(i) > R(j)) -> (i > j),

$Cover relation
forAll i,j:N. (C((i,j)) = 1) <-> ((J((i,j)) = i) /\ !(i = j) /\ !(exists k : N. !(i = k) /\ !(j = k) /\ (J((i, k)) = i) /\ (J((k,j)) = k))),

$Graded
forAll i,j: N. ((J((i,j)) = i) /\ !(i = j)) -> (R(i) > R(j)),
forAll i,j: N. (C((i,j)) = 1) -> (R(i) = R(j) + 1),

$Semi-modular
forAll i,j: N. R(i) + R(j) >= R(M((i,j))) + R(J((i,j))),

$Atomistic
forAll i: N. (R(i) = 1) \/ (R(i) = 0) \/ (exists j,k : N. !(i = k) /\ !(i = j) /\ (J((j,k)) = i)),

$Stabilizer chain constraints
forAll p: int(0..sizeS). exists i:N. (SGS((p,i)) >= i) /\
 ((i = SGS((p,i))) -> ([M((j,i)) | j:int(i+1..n-1)] <=lex [M((j,SGS((p,i)) )) | j:int(i+1..n-1)])),
 
 
 
 
 
 
 
 
 

[M((i,1)) | i:N] >=lex [M((i,2)) | i:N] \/ (R(1) !=R(2)),
[M((i,2)) | i:N] >=lex [M((i,3)) | i:N] \/ (R(2) !=R(3)),
[M((i,3)) | i:N] >=lex [M((i,4)) | i:N] \/ (R(3) !=R(4)),
[M((i,4)) | i:N] >=lex [M((i,5)) | i:N] \/ (R(4) !=R(5)),
[M((i,5)) | i:N] >=lex [M((i,6)) | i:N] \/ (R(5) !=R(6)),
[M((i,6)) | i:N] >=lex [M((i,7)) | i:N] \/ (R(6) !=R(7)),
[M((i,7)) | i:N] >=lex [M((i,8)) | i:N] \/ (R(7) !=R(8)),
[M((i,8)) | i:N] >=lex [M((i,9)) | i:N] \/ (R(8) !=R(9)),
[M((i,9)) | i:N] >=lex [M((i,10)) | i:N] \/ (R(9) !=R(10)),
[M((i,10)) | i:N] >=lex [M((i,11)) | i:N] \/ (R(10) !=R(11)),
[M((i,11)) | i:N] >=lex [M((i,12)) | i:N] \/ (R(11) !=R(12)),
[M((i,12)) | i:N] >=lex [M((i,13)) | i:N] \/ (R(12) !=R(13)),
[M((i,13)) | i:N] >=lex [M((i,14)) | i:N] \/ (R(13) !=R(14)),
[M((i,14)) | i:N] >=lex [M((i,15)) | i:N] \/ (R(14) !=R(15)),
[M((i,15)) | i:N] >=lex [M((i,16)) | i:N] \/ (R(15) !=R(16)),
