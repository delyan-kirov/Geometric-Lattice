language ESSENCE 2.4.0

given n : int

letting N be domain int(0..n-1)

find J : function (total) (N,N) --> N
find M : function (total) (N,N) --> N

such that

$Weekly associative
forAll i,j,k: N. J((i,J((j,k)))) = J((J((i,j)),k)),

$Commutative
forAll i,j: N. J((i,j)) = J((j,i)),
forAll i,j: N. M((i,j)) = M((j,i)),

$Weekly idempotent
forAll i: N. J((i,i)) = i,

$Week absorption law
forAll i,j : N. J((M((i,j)), i)) = i,

$Duality principle
forAll i,j : N. ((J((i,j)) = i) /\ (M((i,j)) = j)) \/ !(J((i,j)) = i),

$Bounded
forAll i : N. M((n-1,i)) = i,
forAll i : N. J((0,i)) = i,

$Ranked by echelon
forAll i,j : N. ((M((i,j)) = i) /\ (i <= j)) \/ 
                ((M((i,j)) = j) /\ (j <= i)) \/
                (M((i,j)) = 0) \/
                                 (forAll k,l : N. (
                                                   ((M((i,k)) = k) /\ (M((j,l)) = l) /\ (i > j) /\ (k >= l)) \/
                                                   ((M((i,k)) = k) /\ (M((j,l)) = l) /\ (i < j) /\ (k <= l)) \/
                                                   !((M((i,k)) = k) \/ (M((j,l)) = l))
                                                   )),

$Semi-modular, Maclane condition 1
forAll i,j,k: N. ((J((j,k)) < i) /\ (i < k) /\ (k < M((i,j))) /\
                 (exists d: N. (
                                (J((j,k)) < d) /\ (d <= j) /\
                                (i = J((M((i,d)),k)))) 
                                )
                 ) \/ !((J((j,k)) < i) /\ (i < k) /\ (k < M((i,j)))),

$Atomistic
forAll i: N. (i = 0) \/
             !(exists j:N. (M((i,j)) = j) /\ !(i = j) /\ !(j = 0)) \/ 
             (exists j,k: N. (J((j,k)) = i) /\ !(j = i) /\ !(k = i))
